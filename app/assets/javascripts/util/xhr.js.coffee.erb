@edsc.util.xhr = do (window, document, $=jQuery) ->
  callbacks = []

  timeoutPool = []
  realSetTimeout = window.setTimeout
  realClearTimeout = window.clearTimeout

  hasPending = ->
    xhrPool.length > 0 || timeoutPool.length > 0

  runCallbacks = ->
    unless hasPending()
      callbacks.pop()() while callbacks.length > 0

  window.setTimeout = (f, args...) ->
    id = null
    onTimeout = ->
      try
        if typeof f == "string"
          eval(f)
        else
          f()
      finally
        index = timeoutPool.indexOf(id)
        timeoutPool.splice(index, 1) if index > -1
        runCallbacks()

    id = realSetTimeout(onTimeout, args...)
    timeoutPool.push(id)
    id

  window.clearTimeout = (id) ->
    index = timeoutPool.indexOf(id)
    timeoutPool.splice(index, 1) if index > -1
    realClearTimeout(id)
    return

  xhrPool = []
  $.ajaxSetup
    beforeSend: (xhr) ->
      xhrPool.push(xhr)
    complete: (xhr) ->
      index = xhrPool.indexOf(xhr)
      xhrPool.splice(index, 1) if (index > -1)
      runCallbacks()

  wait = (callback) ->
    setTimeout((->
      if hasPending()
        callbacks.push(callback)
      else
        callback()), 0)


  exports =
    hasPending: hasPending
    wait: wait
