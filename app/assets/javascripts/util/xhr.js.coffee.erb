@edsc.util.xhr = do (window, document, $=jQuery) ->
  callbacks = []

  timeouts = 0
  xhrs = 0
  timeoutPool = []
  realSetTimeout = window.setTimeout
  realClearTimeout = window.clearTimeout

  hasPending = ->
    [xhrPool.length > 0 || timeoutPool.length > 0, xhrs, timeouts]

  runCallbacks = ->
    unless hasPending()[0]
      callbacks.pop()() while callbacks.length > 0

  window.setTimeout = (f, args...) ->
    id = null
    onTimeout = ->
      timeouts++
      try
        if typeof f == "string"
          eval(f)
        else
          f()
      finally
        index = timeoutPool.indexOf(id)
        timeoutPool.splice(index, 1) if index > -1
        runCallbacks()

    id = realSetTimeout(onTimeout, args...)
    timeoutPool.push(id)
    id

  window.clearTimeout = (id) ->
    index = timeoutPool.indexOf(id)
    timeoutPool.splice(index, 1) if index > -1
    realClearTimeout(id)
    return

  xhrPool = []
  $(document)
    .ajaxSend (event, xhr, settings) ->
      xhrPool.push(xhr)
    .ajaxComplete (event, xhr) ->
      xhrs++
      index = xhrPool.indexOf(xhr)
      xhrPool.splice(index, 1) if (index > -1)
      runCallbacks()

  wait = (callback) ->
    setTimeout((->
      if hasPending()[0]
        callbacks.push(callback)
      else
        callback()), 0)


  exports =
    hasPending: hasPending
    wait: wait
