@edsc.util.xhr = do (window, document, csrfToken=@edsc.config.csrfToken, $=jQuery) ->
  callbacks = []

  xhrs = 0
  timeoutPool = []
  realSetTimeout = window.setTimeout
  realClearTimeout = window.clearTimeout

  hasPending = ->
    xhrPool.length > 0 || timeoutPool.length > 0

  runCallbacks = ->
    while !hasPending() && callbacks.length > 0
      callbacks.pop()()

  window.setTimeout = (f, args...) ->
    id = null
    onTimeout = ->
      try
        if typeof f == "string"
          eval(f)
        else
          f()
      finally
        index = timeoutPool.indexOf(id)
        timeoutPool.splice(index, 1) if index > -1
        runCallbacks()

    id = realSetTimeout(onTimeout, args...)
    timeoutPool.push(id)
    id

  window.clearTimeout = (id) ->
    index = timeoutPool.indexOf(id)
    timeoutPool.splice(index, 1) if index > -1
    realClearTimeout(id)
    return

  xhrPool = []
  $(document)
    .ajaxSend (event, xhr, settings) ->
      xhr.setRequestHeader('X-CSRF-Token', csrfToken)
      xhr.id = xhrs++
      xhrPool.push(xhr.id)
    .ajaxComplete (event, xhr) ->
      index = xhrPool.indexOf(xhr.id)
      xhrPool.splice(index, 1) if (index > -1)
      runCallbacks()

  wait = (callback) ->
    execWhenReady = ->
      callbacks.push(callback)
    setTimeout(execWhenReady, 0)

  exports =
    hasPending: hasPending
    wait: wait
